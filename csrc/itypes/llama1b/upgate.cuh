#pragma once
//
// <NT> RmsUpgateSilu: RMS Norm + UP MatVec + GATE MatVec + SiLU 融合算子
//
// 功能: hidden_states → RMS Norm → UP×silu(GATE) → downproj 输入
//
// 与其他算子的关键区别 (SiLU 两输入问题):
//   SiLU(x) = x * sigmoid(x) = x / (1 + exp(-x))
//   公式需要: up_output 和 gate_output 两个 matvec 结果
//
//   传统方案 (跨 block 依赖):
//     kernel0  Block 0: matvec UP → 写 gmem[up_out]
//              Block 1: matvec GATE → 写 gmem[gate_out] 
//     kernel1  Block 0: 读 gmem[up_out] + 读 gmem[gate_out] → 计算 SiLU
//     问题: Block 1 必须等 Block 0 完成才能读 up_out (跨 block 同步 + gmem 中转)
//
//   本实现方案 (无跨 block 依赖):
//     每个 SM 交替执行 up/gate 两个 tile:
//       iter 0 (up):   matvec UP → up_out (缓存到寄存器)
//       iter 1 (gate): matvec GATE → gate_out → SiLU(gate) → up_out * SiLU(gate)
//       iter 2 (up):   复用寄存器，继续处理下一个 up tile
//     up_out 和 gate_out 在同一个 storer loop 内交替产生和使用，
//     通过寄存器传递，无需 gmem 中转，无需跨 block 同步。
//
// 数据分块:
//   UP/GATE 各有 INTERMEDIATE_DIM / BLOCK_SIZE = 512 / 16 = 32 blocks
//   每个 SM 处理 ceil(32 / sm_count) 个 up+gate 对，共 iters = 2 * blocks_per_sm
//   交错执行: iter 偶数=up, iter 奇数=gate
//

#include "kittens.cuh"
#include "schema.cuh"
#include "utils.cuh"
#include "itypes/llama1b/matvec_pipeline.cuh"

namespace megakittens {

template <typename Config, typename Globals, int N, int SRC_ACT, int SRC_NORM, int SRC_UP, int SRC_GATE, int SCALAR_RMS_EPS, int DST>
struct RmsUpgateSilu {
    struct parsed_instruction {
        int layer_idx, sm_idx, sm_count, total_blocks, iters;
        __device__ inline parsed_instruction(const instruction_t &instruction) {
            layer_idx    = instruction.indices[0];
            sm_idx       = instruction.indices[1];
            sm_count     = instruction.indices[2];
            // <NT> 每个 SM 处理 2 倍的 blocks: up 和 gate 交错执行
            //      例如 sm_count=4, total_blocks=32:
            //        SM 0: block 0,4,8,... (up) 和 block 0,4,8,... (gate) 交替
            //      iters = up_blocks + gate_blocks = 2 * blocks_per_sm
            total_blocks = instruction.indices[3];
            iters        = 2 * ((total_blocks - sm_idx + sm_count - 1) / sm_count);
        }
        // <NT> block_at: 计算第 i 个迭代对应的 block 索引 (偶数 iter=up, 奇数 iter=gate 共用同一 block)
        __device__ inline parsed_instruction(state_t<Config> &s)
            : parsed_instruction(s.instruction()) {}
        __device__ inline int block_at(int i) const {
            return sm_idx + i * sm_count;
        }
    };

    struct pipeline_specifics {
        __device__ static inline void gmem_wait(const Globals &g, state_t<Config> &s) {
            all_input_barrier_wait<Config>(g, s.instruction());
        }

        __device__ static inline void load_iter(state_t<Config> &s, const Globals &g, parsed_instruction &inst,
                  int iter, int col_idx,
                  kittens::st_bf<16, 512> &weight_chunk,
                  kittens::semaphore &sem) {
            int block_idx = inst.block_at(iter / 2);
            if (iter % 2 == 0) {
                kittens::tma::load_async<kittens::dim::ROW, kittens::cache_policy::EVICT_FIRST>(
                    weight_chunk, g.template gls<SRC_UP>(),
                    {inst.layer_idx, block_idx, col_idx}, sem);
            } else {
                kittens::tma::load_async<kittens::dim::ROW, kittens::cache_policy::EVICT_FIRST>(
                    weight_chunk, g.template gls<SRC_GATE>(),
                    {inst.layer_idx, block_idx, col_idx}, sem);
            }
        }

        __device__ static inline void store(state_t<Config> &s, const Globals &g, parsed_instruction &inst,
              int output_idx, int output_stage) {
            // unused, storer inlines to cache up_out
        }
    };

    using pipeline = llama1b::rms_matvec_pipeline<
        Config, Globals, N, parsed_instruction, pipeline_specifics, SRC_ACT, SRC_NORM, SCALAR_RMS_EPS>;
    static_assert(pipeline::OUTPUT_PIPELINE_STAGES == 3);

    struct controller {
        __device__ __forceinline__ static int lid_release_order(const Globals &g, state_t<Config> &s, int query) {
            return pipeline::lid_release_order(g, s, query);
        }
        __device__ __forceinline__ static int init_semaphores(const Globals &g, state_t<Config> &s) {
            return pipeline::init_semaphores(g, s);
        }
    };

    struct loader {
        __device__ __forceinline__ static void run(const Globals &g, state_t<Config> &s) {
            parsed_instruction inst{s};
            pipeline::loader_loop(s, g, inst.layer_idx);
        }
    };

    struct launcher {
        __device__ __forceinline__ static void run(const Globals &g, state_t<Config> &s) {
            pipeline::launcher_loop(s, g);
        }
    };

    struct consumer {
        __device__ __forceinline__ static void run(const Globals &g, state_t<Config> &s) {
            pipeline::consumer_loop(s, g);
        }
    };

    struct storer {
        __device__ __forceinline__ static void run(const Globals &g, state_t<Config> &s) {
            constexpr int OPS = pipeline::OUTPUT_PIPELINE_STAGES;
            parsed_instruction inst{s};
            kittens::rv_fl<16> up_out;
            int output_stage = 0;

            for (int i = 0; i < inst.iters; i++) {
                kittens::wait(pipeline::outputs_arrived(s, output_stage),
                    (i % (2 * OPS)) >= OPS);

                if (i % 2 == 0) {
                    // up iteration: reduce into register now instead of idling
                    llama1b::matvec_reduce<Config, pipeline::SCRATCH_BYTES_PER_WARP>(
                        pipeline::get_output_start(s, output_stage), up_out);
                } else {
                    // gate iteration: up_out already cached in registers
                    int block_idx = inst.block_at(i / 2);
                    uint8_t *scratch = pipeline::get_output_start(s, output_stage);
                    kittens::sv_bf<16> &out_smem = *reinterpret_cast<kittens::sv_bf<16> *>(scratch);

                    kittens::rv_fl<16> gate_out, gate_scratch;
                    llama1b::matvec_reduce<Config, pipeline::SCRATCH_BYTES_PER_WARP>(scratch, gate_out);

                    kittens::warp::mul(gate_scratch, gate_out, -1.f);
                    kittens::warp::exp(gate_scratch, gate_scratch);
                    kittens::warp::add(gate_scratch, gate_scratch, 1.f);
                    kittens::warp::div(gate_out, gate_out, gate_scratch);
                    kittens::warp::mul(gate_out, up_out, gate_out);
                    kittens::warp::sync();
                    kittens::warp::store(out_smem, gate_out);
                    kittens::warp::sync();

                    if (kittens::warp::elect_leader()) {
                        kittens::tma::store_async<kittens::cache_policy::EVICT_LAST>(
                            g.template gls<DST>(), out_smem, {0, block_idx});
                        kittens::tma::store_async_wait();

                        barrier_arrive<Config>(
                            &g.barriers.raw_ptr[s.instruction().dst_barriers[i / 2]], 1);
                    }
                    kittens::warp::sync();
                }

                if ((i + 1) % 2 == 0) {
                    #pragma unroll
                    for (int j = 0; j < 2; j++) {
                        int stage = (output_stage - j + OPS) % OPS;
                        kittens::warp::arrive(pipeline::outputs_finished(s, stage));
                    }
                }
                output_stage = (output_stage + 1) % OPS;
            }

            kittens::warp::sync();
            if (kittens::warp::elect_leader()) {
                kittens::tma::store_async_wait();
                s.page_finish(pipeline::get_activation_page(s));
                all_reuse_barrier_arrive<Config>(g, s.instruction());
            }
        }
    };
};

} // namespace megakittens
