#pragma once

#include "kittens.cuh"

namespace megakittens {

#pragma nv_diag_suppress 940
template <MegaKittensIType Op, WorkerType worker_type, typename T, typename... Args>
__device__ __forceinline__ static T dispatch_instruction(Args &...args) {
    if constexpr      (worker_type == WorkerType::page_manager)      return Op::controller::lid_release_order(args...);
    else if constexpr (worker_type == WorkerType::semaphore_manager) return Op::controller::init_semaphores(args...);
    else if constexpr (worker_type == WorkerType::loader)            return Op::loader::run(args...);
    else if constexpr (worker_type == WorkerType::launcher)          return Op::launcher::run(args...);
    else if constexpr (worker_type == WorkerType::consumer)          return Op::consumer::run(args...);
    else if constexpr (worker_type == WorkerType::storer)            return Op::storer::run(args...);
    else static_assert(sizeof(T) == 9999, "Invalid WorkerType");
}

template <typename Config>
__device__ __forceinline__ static void nanosleep() {
    static_assert(Config::SPIN_LOOP_SLEEP_NS <= 1000000, "nanosleep duration exceeds 1ms");
    asm volatile("{nanosleep.u32 %0;}" :: "r"(Config::SPIN_LOOP_SLEEP_NS));
}

// <NT> barrier 采用的就是最普通的全局内存访问
// * acquire: 读获取，单向栅栏, 本指令之后的所有 ld/st，不能重排到本指令之前
// * release: 写释放，只用于store，本指令之前的所有 ld/st，不能重排到本指令之后
// acquire/release一般配对使用才构成完整内存序。
//
//  PTX后缀	        语义                    典型用途
// .relaxed	  无顺序保证，弱一致           普通内存访问（默认值）
// .acquire	  acquire 语义，后续访问可见   mutex、spinlock
// .release	  release 语义，前置访问可见   mutex、spinlock
// .ca	      cache all (L1+L2)           通常的全局加载
// .cs	      ache streaming              大数据、只读一次
template <typename Config>
__device__ __forceinline__ void barrier_wait(int* barrier_addr, const int target) {
    int barrier_val;
    do {
        asm volatile("{ld.relaxed.gpu.global.u32 %0, [%1];}" // should not spin-loop with acquire
            : "=r"(barrier_val) : "l"(barrier_addr) : "memory"); // TODO: change scope to `sys` for multi-gpu setting
    } while (barrier_val != target);
    asm volatile("{fence.acquire.gpu;}" ::: "memory"); // TODO: change scope to `sys` for multi-gpu setting
}

template <typename Config>
__device__ __forceinline__ void barrier_arrive(int* barrier_addr, const int val) {
    asm volatile("{red.relaxed.gpu.global.add.u32 [%0], %1;}" // TODO: change scope to `sys` for multi-gpu setting
        :: "l"(barrier_addr), "r"(val) : "memory");
}

template <typename Config, typename Globals>
__device__ __forceinline__ void all_input_barrier_wait(const Globals &g, const instruction_t &inst) {
    for (int i = 0; i < inst.num_src_input_barriers; i++)
        barrier_wait<Config>(&g.barriers.raw_ptr[inst.src_barriers[i]], inst.src_barrier_targets[i]);
}

template <typename Config, typename Globals>
__device__ __forceinline__ void all_reuse_barrier_wait(const Globals &g, const instruction_t &inst) {
    for (int i = inst.num_src_input_barriers; i < inst.num_src_input_barriers + inst.num_src_reuse_barriers; i++)
        barrier_wait<Config>(&g.barriers.raw_ptr[inst.src_barriers[i]], inst.src_barrier_targets[i]);
}

template <typename Config, typename Globals>
__device__ __forceinline__ void all_input_barrier_arrive(const Globals &g, const instruction_t &inst) {
    for (int i = 0; i < inst.num_dst_input_barriers; i++)
        barrier_arrive<Config>(&g.barriers.raw_ptr[inst.dst_barriers[i]], 1);
}

template <typename Config, typename Globals>
__device__ __forceinline__ void all_reuse_barrier_arrive(const Globals &g, const instruction_t &inst) {
    for (int i = inst.num_dst_input_barriers; i < inst.num_dst_input_barriers + inst.num_dst_reuse_barriers; i++)
        barrier_arrive<Config>(&g.barriers.raw_ptr[inst.dst_barriers[i]], 1);
}

template <typename Config, typename Globals>
__device__ __forceinline__ void all_barrier_arrive(const Globals &g, const instruction_t &inst) {
    for (int i = 0; i < inst.num_dst_input_barriers + inst.num_dst_reuse_barriers; i++)
        barrier_arrive<Config>(&g.barriers.raw_ptr[inst.dst_barriers[i]], 1);
}

} // namespace megakittens
